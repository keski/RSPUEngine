# FILTER only queries
# Q1: Two streams, vary filter selectivity to either increase or decrease the number of JOIN partners near filters
# Q2: Two streams, vary filter selectivity to either increase or decrease the number of JOIN partners near filters. Complex filter requiring simulation (expensive!).
# Q3: Four streams, vary filter selectivity to either increase or decrease the number of JOIN partners near filters
# Q4: Four streams, vary filter selectivity to either increase or decrease the number of JOIN partners near filters. Complex filter requiring simulation (expensive!).

# BIND only queries
# Q5: Two streams, vary filter selectivity to either increase or decrease the number of JOIN partners near filters
# Q6: Two streams, vary filter selectivity to either increase or decrease the number of JOIN partners near filters. Complex filter requiring simulation (expensive!).
# Q7: Four streams, vary filter selectivity to either increase or decrease the number of JOIN partners near filters
# Q8: Four streams, vary filter selectivity to either increase or decrease the number of JOIN partners near filters. Complex filter requiring simulation (expensive!).

# BIND-FILTER queries
# Q9: Two streams, vary filter selectivity to either increase or decrease the number of JOIN partners near filters
# Q10: Two streams, vary filter selectivity to either increase or decrease the number of JOIN partners near filters. Complex filter requiring simulation (expensive!).
# Q11: Four streams, vary filter selectivity to either increase or decrease the number of JOIN partners near filters
# Q12: Four streams, vary filter selectivity to either increase or decrease the number of JOIN partners near filters. Complex filter requiring simulation (expensive!).


# Query description:
# For all tunnel segments, generate a notification when the probability that the two sensors have reported
# an oxygen concentration threshold violation with some probability.
#
# Modes:
# - Lazy+cache: on/off
# - H5: on/off
#
# Parameters to vary:
# - Probability threshold of filters (0, 0.1, 0.2, 0.3, ..., 0.7)
#
# Note: Selectivity is at most P(x<threshold) * P(x<threshold))
#
# Data: 5% of all reported values are below threshold.
#

BASE <http://example.org/data/>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX rspu: <http://w3id.org/rsp/rspu#>
PREFIX : <http://example.org/ontology/>

REGISTER STREAM <q1> COMPUTED EVERY PT2S AS
SELECT ?foi ?value1 ?mu1 ?value2 ?mu2
FROM NAMED WINDOW <w1> ON <http://example.org/ox1> [RANGE PT10S STEP PT2S]
FROM NAMED WINDOW <w2> ON <http://example.org/ox2> [RANGE PT10S STEP PT2S]
WHERE {
    ?sensor1 a :OxygenSensor1 ;
        sosa:hasFeatureOfInterest ?foi ;
        rspu:measurementUncertainty ?mu1 .
    ?sensor2 a :OxygenSensor2 ;
        sosa:hasFeatureOfInterest ?foi .

    WINDOW <w1> {
        GRAPH ?g1 {
            ?o1 sosa:hasSimpleResult ?value1 ;
                sosa:madeBySensor ?sensor1;
                sosa:observedProperty ?property1 .
        }
    }
    WINDOW <w2> {
        GRAPH ?g2 {
            ?o2 sosa:hasSimpleResult ?value2 ;
                sosa:madeBySensor ?sensor2 ;
                sosa:observedProperty ?property1 .
            << ?o2 sosa:hasSimpleResult ?value2 >> rspu:measurementUncertainty ?mu2 .
        }
    }

    # The reported values should be within 1% from each other
    FILTER(abs(?value1 - ?value2) < 0.001)

    # There should be a non-zero probability that both observed values are below the scenario threshold
    FILTER(rspu:lessThan(rspu:add(?mu1, ?value1), 0.18) > 0.1)
    FILTER(rspu:lessThan(rspu:add(?mu2, ?value2), 0.18) > 0.1)
}