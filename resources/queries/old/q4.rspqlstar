# PARAMETERS:
# - P_1 : [0,1)
# - P_2 : [0,1)

BASE <http://example.org/data/>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX rspu: <http://w3id.org/rsp/rspu#>
PREFIX : <http://example.org/ontology/>

REGISTER STREAM <q4> COMPUTED EVERY PT1S AS
SELECT ?foi ?value1 ?mu1 ?value2 ?mu2 ?value3 ?mu3 ?value4 ?mu4
FROM NAMED WINDOW <w1> ON <http://example.org/ox1> [RANGE PT1S STEP PT1S]
FROM NAMED WINDOW <w2> ON <http://example.org/ox2> [RANGE PT1S STEP PT1S]
FROM NAMED WINDOW <w3> ON <http://example.org/temp1> [RANGE PT1S STEP PT1S]
FROM NAMED WINDOW <w4> ON <http://example.org/temp2> [RANGE PT1S STEP PT1S]
WHERE {
    ?sensor1 a :OxygenSensor1 ;
        sosa:hasFeatureOfInterest ?foi ;
        rspu:measurementUncertainty ?mu1 .
    ?sensor2 a :OxygenSensor2 ;
        sosa:hasFeatureOfInterest ?foi .
    ?sensor3 a :TemperatureSensor1  ;
        sosa:hasFeatureOfInterest ?foi ;
        rspu:measurementUncertainty ?mu3 .
    ?sensor4 a :TemperatureSensor2  ;
        sosa:hasFeatureOfInterest ?foi .

    WINDOW <w1> {
        GRAPH ?g1 {
            ?o1 sosa:hasSimpleResult ?value1 ;
                sosa:madeBySensor ?sensor1;
                sosa:observedProperty ?property1 .
        }
    }
    WINDOW <w2> {
        GRAPH ?g2 {
            ?o2 sosa:hasSimpleResult ?value2 ;
                sosa:madeBySensor ?sensor2 ;
                sosa:observedProperty ?property1 .
            << ?o2 sosa:hasSimpleResult ?value2 >> rspu:measurementUncertainty ?mu2
            FILTER(abs(?value1 - ?value2) > 0.2)

            # The RVs should not be "different" with a probability greater than P
            # P(X > Y) = P(X - Y > 0)
            FILTER(rspu:greaterThan(
                rspu:subtract(
                    rspu:add(?mu1, ?value1), rspu:add(?mu2, ?value2)
               ), 0) > $P)
        }
    }
    WINDOW <w3> {
        GRAPH ?g3 {
            ?o3 sosa:hasSimpleResult ?value3 ;
                sosa:madeBySensor ?sensor3;
                sosa:observedProperty ?property2 .
        }
    }
    WINDOW <w4> {
        GRAPH ?g4 {
            ?o4 sosa:hasSimpleResult ?value4 ;
                sosa:madeBySensor ?sensor4 ;
                sosa:observedProperty ?property2 .
            << ?o4 sosa:hasSimpleResult ?value4 >> rspu:measurementUncertainty ?mu4
            FILTER(abs(?value3 - ?value4) > 4)

            # The RVs should not be "different" with a probability greater than P
            # P(X > Y) = P(X - Y > 0)
            FILTER(rspu:greaterThan(
                rspu:subtract(
                    rspu:add(?mu3, ?value3), rspu:add(?mu4, ?value4)
               ), 0) > $P)
        }
    }

    # Check for value1 > value2
    #FILTER(?value3 < ?value4)

    FILTER(rspu:greaterThan(rspu:add(?mu3, ?value3), rspu:add(?mu4, ?value4)) > $P)
}