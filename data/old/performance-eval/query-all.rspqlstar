# Query demonstrating all uncertainty types over a single event stream.
# - Fuzzy: The degree to which either HighHeartRateEvent OR VeryHighHeartRateEvent is true
# - Probability: The probability that the heart rate is above the threshold of 100 beats/min
# - Bayes: The most likely medical status (Normal or Abnormal) given the heart-rate state as evidence.

BASE <http://base/>
PREFIX ecare: <http://www.example.org/ecare#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX rspu: <http://w3id.org/rsp/rspu#>
PREFIX bn-ns: <http://w3id.org/rsp/rspu/bn#>

REGISTER STREAM <all> COMPUTED EVERY PT1S AS

SELECT ?event ?hr ?state ?degree1 ?degree2 ?unc1 ?unc2 ?unc3 ?unc4
FROM NAMED WINDOW <w> ON <http://ecareathome/stream/heartrate> [RANGE PT1S STEP PT1S]
WHERE {
   WINDOW <w> {
      GRAPH ?g {
          ?event a ecare:HeartRateEvent .
          ?event sosa:hasSimpleResult ?hr .
          << ?event a ?node >> rspu:hasState ?state .
          << ?event a ecare:HighHeartRateEvent >> rspu:degreeOfTruth ?degree1 .
          << ?event a ecare:VeryHighHeartRateEvent >> rspu:degreeOfTruth ?degree2 .

          # Fuzzy
          BIND(rspu:disjunction(?degree1, ?degree2) AS ?unc1)
          # Probability
          BIND(rspu:greaterThan(?hr, 100) AS ?unc2)
          # Bayes
          BIND(rspu:map(<http://example.org/bn/medical-small>, ecare:MedicalStatus, ?node, ?state) AS ?unc3)
          # Bayes large
          BIND(URI(CONCAT("http://www.example.org/ecare#x", STR(xsd:integer(ROUND(RAND()*9)) * 2 + 2), "mmol_l")) AS ?obs)
          BIND(rspu:map(<http://example.org/bn/medical-large>, ecare:bg_10, ecare:bg_20, ?obs) AS ?unc4)
      }
      ?g ?p ?time .
   }
}