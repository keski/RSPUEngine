# Correctness query. Not optimized for performance.
# Determines TP, TN, FP, FN

BASE <http://base/>
PREFIX : <http://ecare#>
PREFIX rspu: <http://w3id.org/rsp/rspu#>
PREFIX sosa: <http://www.w3.org/ns/sosa/>

REGISTER STREAM <correctness> COMPUTED EVERY PT1S AS
SELECT ?foi ("occ" AS ?type) ($OCCURRENCE$ AS ?occ) ($ATTRIBUTE$ AS ?attribute) ?threshold ?tp ?tn ?fp ?fn
FROM NAMED WINDOW <w1> ON <stream/hr> [RANGE PT1S STEP PT1S]
FROM NAMED WINDOW <w2> ON <stream/br> [RANGE PT1S STEP PT1S]
FROM NAMED WINDOW <w3> ON <stream/ox> [RANGE PT1S STEP PT1S]
FROM NAMED WINDOW <w4> ON <stream/ha> [RANGE PT1S STEP PT1S]
WHERE {
    WINDOW <w1> {
        GRAPH ?g1 {
            ?id1 sosa:featureOfInterest ?foi ;
                 sosa:hasSimpleResult ?hr .
            << ?id1 a :HighHeartRateEvent >> rspu:probability ?pHR .
        }
    }
    WINDOW <w2> {
        GRAPH ?g2 {
            ?id2 sosa:featureOfInterest ?foi ;
                 sosa:hasSimpleResult ?br .
            << ?id2 a :HighBreathingRateEvent >> rspu:probability ?pBR .
        }
    }
    WINDOW <w3> {
        GRAPH ?g3 {
             ?id3 sosa:featureOfInterest ?foi ;
                  sosa:hasSimpleResult ?ox .
             << ?id3 a :LowOxygenSaturationEvent >> rspu:probability ?pOX .

        }
    }
    WINDOW <w4> {
        GRAPH ?g4 {
            ?id4 sosa:featureOfInterest ?foi .
            << ?id4 a :HeartAttackEvent >> rspu:probability ?pHA .
        }
    }
    BIND(rspu:belief(<http://ecare/bn#heart-attack>,
        :HeartAttackEvent, true,
        :HighHeartRateEvent, ?pHR,
        :HighBreathingRateEvent, ?pBR,
        :LowOxygenSaturationEvent, ?pOX) AS ?pTotal)

    BIND($THRESHOLD$ AS ?threshold)
    BIND(?pTotal >= ?threshold AS ?predicted)
    BIND(?pHA = 1 AS ?actual)
    BIND(?predicted && ?actual AS ?tp)
    BIND(?predicted=false && ?actual=false AS ?tn)
    BIND(?predicted && ?actual=false AS ?fp)
    BIND(?predicted=false && ?actual AS ?fn)
}