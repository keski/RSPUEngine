# Correctness query: option 5
# Description: Pattern uncertainty with attribute uncertainty.
# Stream type:
# - attribute uncertainty: 0, .01, .05, .10, .25
# - occurrence uncertainty: 0
# - stream rate: 1000 events/s per stream
# Determines: TP, TN, FP, FN

BASE <http://base/>
PREFIX : <http://ecare#>
PREFIX rspu: <http://w3id.org/rsp/rspu#>
PREFIX sosa: <http://www.w3.org/ns/sosa/>

REGISTER STREAM <correctness> COMPUTED EVERY PT1S AS
SELECT ?pTot ?actual ?foi ("option5" AS ?type) ?threshold ?tn ?tp ?fp ?fn
($OCCURRENCE$ AS ?occurrence) ($ATTRIBUTE$ AS ?attribute)
FROM NAMED WINDOW <w1> ON <stream/hr> [RANGE PT1S STEP PT1S]
FROM NAMED WINDOW <w2> ON <stream/br> [RANGE PT1S STEP PT1S]
FROM NAMED WINDOW <w3> ON <stream/ox> [RANGE PT1S STEP PT1S]
# oracle stream
FROM NAMED WINDOW <w4> ON <stream/ce> [RANGE PT1S STEP PT1S]
WHERE {
    WINDOW <w1> {
        GRAPH ?g1 {
            ?id1 sosa:featureOfInterest ?foi ;
                 a :HeartRate .
            << ?id1 sosa:hasSimpleResult ?hr >> rspu:error ?hrError .
        }
    }
    WINDOW <w2> {
        GRAPH ?g2 {
            ?id2 sosa:featureOfInterest ?foi ;
                 a :BreathingRate .
            << ?id2 sosa:hasSimpleResult ?br >> rspu:error ?brError .
        }
    }
    WINDOW <w3> {
        GRAPH ?g3 {
             ?id3 sosa:featureOfInterest ?foi ;
                  a :OxygenSaturation .
            << ?id3 sosa:hasSimpleResult ?ox >> rspu:error ?oxError .
        }
    }
    WINDOW <w4> {
        GRAPH ?g4 {
            ?id4 sosa:featureOfInterest ?foi .
            << ?id4 a :COPDExacerbation >> rspu:probability ?pCe .
        }
    }

    BIND(rspu:greaterThan(rspu:add(?hrError, ?hr), 100) AS ?pHr)
    BIND(rspu:greaterThan(rspu:add(?brError, ?br), 30) AS ?pBr)
    BIND(rspu:lessThan(rspu:add(?oxError, ?ox), 90) AS ?pOx)

    BIND($THRESHOLD$ AS ?threshold)
    BIND(rspu:belief(<http://ecare/bn#medical>,
        :COPDExacerbation, true,
        :HighHeartRate, ?pHr,
        :HighBreathingRate, ?pBr,
        :LowOxygenSaturation, ?pOx) AS ?pTot)

    BIND(?pTot >= ?threshold AS ?predicted)
    BIND(?pCe = 1 AS ?actual)
    BIND(?predicted && ?actual AS ?tp)
    BIND(?predicted=false && ?actual=false AS ?tn)
    BIND(?predicted && ?actual=false AS ?fp)
    BIND(?predicted=false && ?actual AS ?fn)
}