# Correctness query: option 3
# Description: Pattern uncertainty. Observations are simply assumed to be true if the probability is greater than 0.5 (MLE)
# Stream type:
# - attribute uncertainty: 0
# - occurrence uncertainty: 0, .01, .05, .10, .25
# - stream rate: 1000 events/s per stream
# Determines: TP, TN, FP, FN

BASE <http://base/>
PREFIX : <http://ecare#>
PREFIX rspu: <http://w3id.org/rsp/rspu#>
PREFIX sosa: <http://www.w3.org/ns/sosa/>

REGISTER STREAM <correctness> COMPUTED EVERY PT1S AS
SELECT ?pTot ?actual ?foi ("option3" AS ?type) ?threshold ?tn ?tp ?fp ?fn
($OCCURRENCE$ AS ?occurrence) ($ATTRIBUTE$ AS ?attribute)
FROM NAMED WINDOW <w1> ON <stream/hr> [RANGE PT1S STEP PT1S]
FROM NAMED WINDOW <w2> ON <stream/br> [RANGE PT1S STEP PT1S]
FROM NAMED WINDOW <w3> ON <stream/ox> [RANGE PT1S STEP PT1S]
# oracle stream
FROM NAMED WINDOW <w4> ON <stream/ce> [RANGE PT1S STEP PT1S]
WHERE {
    WINDOW <w1> {
        GRAPH ?g1 {
            ?id1 sosa:featureOfInterest ?foi .
            << ?id1 a :HighHeartRate >> rspu:probability ?pHr .
        }
    }
    WINDOW <w2> {
        GRAPH ?g2 {
            ?id2 sosa:featureOfInterest ?foi .
            << ?id2 a :HighBreathingRate >> rspu:probability ?pBr .
        }
    }
    WINDOW <w3> {
        GRAPH ?g3 {
            ?id3 sosa:featureOfInterest ?foi .
            << ?id3 a :LowOxygenSaturation >> rspu:probability ?pOx .
        }
    }
    WINDOW <w4> {
        GRAPH ?g4 {
            ?id4 sosa:featureOfInterest ?foi .
            << ?id4 a :COPDExacerbation >> rspu:probability ?pCe .
        }
    }

    BIND($THRESHOLD$ AS ?threshold)
    BIND(rspu:belief(<http://ecare/bn#medical>,
        :COPDExacerbation, true,
        :HighHeartRate, IF(?pHr > .5, 1, 0),
        :HighBreathingRate, IF(?pBr > .5, 1, 0),
        :LowOxygenSaturation, IF(?pOx > .5, 1, 0)) AS ?pTot)

    BIND(?pTot >= ?threshold AS ?predicted)
    BIND(?pCe = 1 AS ?actual)
    BIND(?predicted && ?actual AS ?tp)
    BIND(?predicted=false && ?actual=false AS ?tn)
    BIND(?predicted && ?actual=false AS ?fp)
    BIND(?predicted=false && ?actual AS ?fn)
}