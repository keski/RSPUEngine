# Performance query: option 0 (baseline)
# Description: Filter by values
# Stream type:
# - attribute uncertainty: .05
# - occurrence uncertainty: 0
# - stream rate: varies

BASE <http://base/>
PREFIX : <http://ecare#>
PREFIX rspu: <http://w3id.org/rsp/rspu#>
PREFIX sosa: <http://www.w3.org/ns/sosa/>

REGISTER STREAM <performance> COMPUTED EVERY PT1S AS
SELECT ($RATE$ AS ?rate) ("baseline" AS ?type) (COUNT(*) AS ?count)
FROM NAMED WINDOW <w1> ON <stream/hr> [RANGE PT1S STEP PT1S]
FROM NAMED WINDOW <w2> ON <stream/br> [RANGE PT1S STEP PT1S]
FROM NAMED WINDOW <w3> ON <stream/ox> [RANGE PT1S STEP PT1S]
WHERE {
    WINDOW <w1> {
        GRAPH ?g1 {
            ?id1 sosa:featureOfInterest ?foi ;
                 a :HeartRate ;
                 sosa:hasSimpleResult ?hr .
        }
    }
    WINDOW <w2> {
        GRAPH ?g2 {
            ?id2 sosa:featureOfInterest ?foi ;
                 a :BreathingRate ;
                 sosa:hasSimpleResult ?br .
        }
    }
    WINDOW <w3> {
        GRAPH ?g3 {
             ?id3 sosa:featureOfInterest ?foi ;
                  a :OxygenSaturation ;
                  sosa:hasSimpleResult ?ox .
        }
    }

    BIND( ?hr > 100 && ?br > 30 && ?ox < 90 AS ?confidence)
}